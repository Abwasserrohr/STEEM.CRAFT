#
# ==============
# bossbar.sk
# ==============
# bossbar.sk is part of the STEEM.CRAFT library.
# ==============

#
# > On some Skript versions, this needs an extra reload to work.
on skript load:
  wait 5 seconds
  execute console command "/sk reload STEEM.CRAFT/_lib/bossbar.sk"

import:
  org.bukkit.Bukkit
  org.bukkit.boss.BarStyle
  org.bukkit.boss.BarColor

#
# > Function - createBossBar
# > Creates a bossbar which can be used.
# > Parameters:
# > <text> title which should be displayed
# > [<text>] bossbar color - Options: "blue","green","pink","purple","red","white","yellow"
# > [<text>] bossbar segments - Options: "solid","6","10","12","20"
function createBossBar(title:text,barcolor:text="white",barstyle:text="solid") :: object:
  #
  # > Creates a bar color for the boss bar.
  set {_bc} to BarColor.valueOf({_barcolor}.toUpperCase())

  #
  # > Creates a bar style to the boss bar. This is done to allow
  # > the server operator easily calling the function with text.
  if {_barstyle} is "solid":
    set {_bs} to BarStyle.SOLID!
  if {_barstyle} is "SEGMENTED_10" or "10":
    set {_bs} to BarStyle.SEGMENTED_10!
  if {_barstyle} is "SEGMENTED_12" or "12":
    set {_bs} to BarStyle.SEGMENTED_12!
  if {_barstyle} is "SEGMENTED_20" or "20":
    set {_bs} to BarStyle.SEGMENTED_20!
  if {_barstyle} is "SEGMENTED_6" or "6":
    set {_bs} to BarStyle.SEGMENTED_6!

  #
  # > Return the created boss bar.
  return Bukkit.createBossBar({_title}, {_bc}, {_bs}, null)
