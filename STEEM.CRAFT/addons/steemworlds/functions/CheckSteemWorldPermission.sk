#
# ==============
# CheckSteemWorldPermission.sk
# ==============
# CheckSteemWorldPermission.sk is part of the STEEM.CRAFT addons.
# ==============

#
# > Function - CheckSteemWorldPermission
# > Returns if the player has permissions for the defined world.
# > Parameters:
# > <world>the world name
# > <player>the player who should get checked for permission
# > [<text>]the type of permission which should be checked (default="build")
# > [<boolean>]false = the player will not get a error message for not being allowed (default=true)
function CheckSteemWorldPermission(world:world,player:player,permissiontype:text="build",senderror:boolean=true) :: boolean:
  set {_steemaccount} to getSyncedAccount({_player})
  #
  # > Currently, there is no real permission system behind. If the name of
  # > the world matches which the synced username, the user has permission
  # > to edit the world.
  set {_check} to GetSteemWorldShortName({_world})
  replace all "%{_steemaccount}%-" with "" in {_check}
  if "%{_world}%" is not "steemworlds-%{_steemaccount}%-%{_check}%":
    if {_senderror} is true:
      message "%getChatPrefix()% This action is only available to the owner of the world." to {_player}
    return false
  return true
