#
# ==============
# steemworlds/teleportsigns.sk v0.0.4
# ==============
# steemworlds/teleportsigns.sk is part of the STEEM.CRAFT addons (addon to steemworlds.sk).
# ==============
# > teleportsigns allows players to create signs which
# > send players to a specific steemworld by clicking on it.
# ==============
# > Usage:
# > 1: [Steem]
# > 2: Steem username
# > 3: World name
# > 4: empty
# ==============

#
# > Event - on sign change
# > Triggered if a sign is placed and text has been saved.
# > Actions:
# > The username and permlink is going to be checked. If both are
# > existing, it will be formatted and clickable.
on sign change:
  #
  # > Only detect signs which match with the detectline option.
  if line 1 is scswGetConfig("teleportsigns_detectline"):
    #
    # > Get line 2 and line 3 with lowercase characters.
    set {_name} to (line 2).toLowerCase()
    set {_worldname} to (line 3).toLowerCase()

    #
    # > If the given username is not set, tell the that the 2nd
    # > line needs a steem username. Also break the sign again.
    # > Load the world comment.
    getAccount({_name})

    #
    # > Wait until the Steem account response is no longer "wait".
    while getAccountResponse({_name}) is "wait":
      wait 1 tick

    #
    # > Get the Steem account response.
    set {_account} to getAccountResponse({_name})

    if {_account} is not set:
      message "%getChatPrefix()% Set a valid Steem name on the 2nd line."
      event-block.breakNaturally()
      stop

    #
    # > Create a permlink which can be used to get the world to check
    # > if it actually exists.
    set {_permlink} to "re-steemcraft-steemworlds-%{_name}%-%{_worldname}%"

    #
    # > Load the world comment.
    getSteemContent({_name},{_permlink})

    #
    # > Wait until the Steem content response is no longer "wait".
    while getSteemContentResponse({_name},{_permlink}) is "wait":
      wait 1 tick

    #
    # > Get the Steem content response.
    set {_c} to getSteemContentResponse({_name},{_permlink})

    #
    # > If the author of the world is not set, it doesn't exist. Tell the
    # > player that a valid world name on the 3rd line is necessary.
	# > Also, break the sign naturally.
    if {_c}.getAuthor().getName() is not set:
      message "%getChatPrefix()% Set a valid world name on the 3rd line."
      event-block.breakNaturally()
      stop

    #
    # > Since this is executed, no errors have occured above, format the
    # > sign to make it clickable.
	
    set line 1 to "%scswGetConfig(""teleportsigns_signhead"")%"
    set line 4 to "%scswGetConfig(""teleportsigns_footer"")%"

#
# > Event - on rightclick on sign
# > Triggered if someone rightclicks on a sign.
# > Actions:
# > Calls the /steemworldvisit command for the player,
# > which teleports the player to the world, if the
# > sign has the right format.
on rightclick on sign:
  if line 1 of event-block is scswGetConfig("teleportsigns_signhead"):
    make player execute "/steemworldvisit %line 2% %line 3%"
