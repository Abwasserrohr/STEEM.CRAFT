#
# ==============
# notifications.sk v0.0.4
# ==============
# notifications.sk is part of the STEEM.CRAFT addons.
# ==============
# > notifications.sk allows players to get a notification if something
# > happens that involves their account name on steem in new blocks.
# ==============

#
# > Options
# > These settings can be changed to fit your needs.
options:
  #
  # > Enable or disable the notifications here. Instead of disabling it
  # > here, adding a "-" prefix will also disable it on the next reload.
  enable-notifications: true

  #
  # > Set the notification sound for comments here.
  # > Format: ["sound name",volume,pitch]
  # > To disable: ["",0,0]
  commentnotificationsound: ["entity.player.levelup",1,0.5]

  #
  # > Set the notification sound for comments here.
  # > Format: ["sound name",volume,pitch]
  # > To disable: ["",0,0]
  votenotificationsound: ["",0,0]

  #
  # > Set the notification sound for comments here.
  # > Format: ["sound name",volume,pitch]
  # > To disable: ["",0,0]
  transfernotificationsound: ["entity.player.levelup",1,0.5]

import:
  com.fasterxml.jackson.databind.ObjectMapper
  java.util.ArrayList
  net.md_5.bungee.api.chat.ComponentBuilder
  net.md_5.bungee.api.chat.TextComponent
  net.md_5.bungee.api.chat.BaseComponent
  net.md_5.bungee.api.chat.ClickEvent
  net.md_5.bungee.api.chat.HoverEvent
  net.md_5.bungee.api.chat.HoverEvent$Action as HoverEventAction
  net.md_5.bungee.api.chat.ClickEvent$Action as ClickEventAction


#
# > Event - on load
# > Saves the current head block number to memory for new block checks.
on load:
  wait 1 tick
  UpdateHeadBlockNumber()

function UpdateHeadBlockNumber():
  set {_mapper} to new ObjectMapper()

  set {_request} to HashMap()

  {_request}.put("jsonrpc", "2.0")
  {_request}.put("method", "condenser_api.get_dynamic_global_properties")
  {_request}.put("params", new ArrayList())
  {_request}.put("id", 1)

  set {_requestID} to getRandomUUID()
  CustomSteemRequest({_mapper}.writeValueAsString({_request}),{_requestID})

  while CustomSteemRequestResult({_requestID}) is "wait":
    wait 1 tick

  set {_response} to CustomSteemRequestResult({_requestID})
  set {_headblocknumber} to {_response}.get("result").get("head_block_number").doubleValue()

  set metadata value "SC|HeadBlockNumber" of getDummy() to {_headblocknumber}
  set {SC::HeadBlockNumber} to {_headblocknumber}

every 3 seconds:
  if {@enable-notifications} is true:
    $ thread
    runBlockNotificationCheck()

#
# > Function - runBlockNotificationCheck
# > This function should be called in a thread and will
# > check for notifiactions for currently online steem
# > users to get a message and sound.
function runBlockNotificationCheck():

  set {_blocknumber} to {SC::HeadBlockNumber}
  add 1 to {_blocknumber}

  if {_blocknumber} is set:

    #
    # > Set the metadata value to mark this block as the last processed
    # > steem block. This helps to prevent the processing of the same
    # > block multiple times.
    set metadata value "SC|HeadBlockNumber" of getDummy() to {_blocknumber}
    set {SC::HeadBlockNumber} to {_blocknumber}

    #
    # > Get all operations of the block.
    set {_id} to getRawBlock({_blocknumber})
    while CustomSteemRequestResult({_id}) is "wait":
      wait 1 tick
    set {_response} to CustomSteemRequestResult({_id})

    set {_transactions} to try {_response}.get("result").get("transactions")

    #
    # > If the transactions aren't set, then update the head block number and stop.
    if "%try {_transactions}.size()%" is "<none>":
      UpdateHeadBlockNumber()
      stop

    #
    # > Get all Steem users that are currently online on this server to check
    # > if we need to notify the user about something going on on the Steem
    # > blockchain. E.g. transfer, comment, vote.
    # > Adding a check for scot token would be nice later on.
    set {_onlineusers::*} to ...metadata value "onlineSteemUsers" of getDummy()
    send action bar "%{_blocknumber}% - %{_transactions}.size()%" to "Immanuel94" parsed as player

    #
    # > Loop through all transactions that happened in this block.
    loop {_transactions}.size() times:

      # 
      # > Select the currently looped operation.
      set {_operations} to {_transactions}.get(loop-number - 1)

      set {_loop} to -1
      loop {_operations}.get("operations").size() times:
        add 1 to {_loop}
        set {_operation} to {_operations}.get("operations").get({_loop})
        set {_optype} to {_operation}.get(0).textValue()
        #
        # > If the operation is a CoommentOperation.
        if {_optype} is "comment":

          #
          # > If the parent author isn't a empty string (blog post).
          if {_operation}.get(1).get("parent_author").textValue() is not "":

            #
            # > Loop through the online users and check if the parent author is
            # > currently online on our server.
            loop {_onlineusers::*}:

              #
              # > If the looped online steem player is the parent author of this comment.
              if loop-value-3 contains "%{_operation}.get(1).get(""parent_author"").textValue()%|":
			  
                #
                # > If the looped online steem player is not the author of this comment.
                if loop-value-3 doesn't contain "%{_operation}.get(1).get(""author"").textValue()%|":

                  #
                  # > Get the parsed player out of the looped player string.
                  set {_player::*} to loop-value-3 split at "|"
                  set {_player} to {_player::2} parsed as player

                  #
                  # > Get the name of the author for the notification message.
                  set {_commenter} to {_operation}.get(1).get("author").textValue()

                  #
                  # > Get the predefined sound from the options parsed as list.
                  set {_sound::*} to ...{@commentnotificationsound}

                  #
                  # > If the first sound string is not a empty string, go forward.
                  if {_sound::1} is not "":

                    #
                    # > Play a sound to the player who gets the notification.
                    play sound {_sound::1} with volume {_sound::2} with pitch {_sound::3} at location of {_player} for {_player}

                  set {_body} to {_operation}.get(1).get("body").textValue()
                  set {_permlink} to {_operation}.get(1).get("permlink").textValue()

                  #
                  # > Send the actual notification message.
                  message "%getChatPrefix()% %{_commenter}% replied to you:" to {_player}
                  message "%getChatPrefix()% %{_body}%" to {_player}
                  set {_message} to new ComponentBuilder("%getChatPrefix()% ")

                  set {_component} to new TextComponent("[&2&lVote&7] ")
                  {_component}.setClickEvent(new ClickEvent(ClickEventAction.RUN_COMMAND!, "/steem vote %{_commenter}% %{_permlink}% "))
                  {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to vote on the reply." ).create()))
                  {_message}.append({_component})

                  set {_component} to new TextComponent("[&2&lReply&7]")
                  {_component}.setClickEvent(new ClickEvent(ClickEventAction.SUGGEST_COMMAND!, "/steem comment %{_commenter}% %{_permlink}% "))
                  {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to reply." ).create()))
                  {_message}.append({_component})

                  #
                  # > Send the clickable text to the player.
                  {_player}.spigot().sendMessage({_message}.create())

        #
        # > If the operation is a VoteOperation.
        else if {_optype} is "vote":

          #
          # > Loop through the online users and check if the author which has
          # > recieved the vote is online on the server.
          loop {_onlineusers::*}:

            #
            # > If the looped online steem player is the author of the post which has been voted.
            if loop-value-3 contains "%{_operation}.get(1).get(""author"").textValue()%|":

              #
              # > If the looped online steem player is not the voter himself.
              if loop-value-3 doesn't contain "%{_operation}.get(1).get(""voter"").textValue()%|":

                #
                # > Get the voter for the final notifiaction message.
                set {_voter} to {_operation}.get(1).get("voter").textValue()
			  
                #
                # > Get the parsed player out of the looped player string.
                set {_player::*} to loop-value-3 split at "|"
                set {_player} to {_player::2} parsed as player
			
                #
                # > Get the predefined sound from the options parsed as list.
                set {_sound::*} to ...{@votenotificationsound}

                #
                # > If the first sound string is not a empty string, go forward.
                if {_sound::1} is not "":

                  #
                  # > Play a sound to the player who gets the notification.
                  play sound {_sound::1} with volume {_sound::2} with pitch {_sound::3} at location of {_player} for {_player}

			
                message "%getChatPrefix()% %{_voter}% voted on your post." to {_player}

        #
        # > If the operation is a TransferOperation.
        else if {_optype} is "transfer":

          #
          # > Loop through the online users and check if the sender or the
          # > target of the transfer is currently online on our server.
          loop {_onlineusers::*}:

            #
            # > If the looped online steem player has revieved a transfer.
            if loop-value-3 contains "%{_operation}.get(1).get(""to"").textValue()%|":

              #
              # > Get the parsed player out of the looped player string.
              set {_player::*} to loop-value-3 split at "|"
              set {_player} to {_player::2} parsed as player

              #
              # > Get the predefined sound from the options parsed as list.
              set {_sound::*} to ...{@transfernotificationsound}

              #
              # > If the first sound string is not a empty string, go forward.
              if {_sound::1} is not "":

                #
                # > Play a sound to the player who gets the notification.
                play sound {_sound::1} with volume {_sound::2} with pitch {_sound::3} at location of {_player} for {_player}

              #
              # > Get the amount, memo and the sender.
              set {_amount} to {_operation}.get(1).get("amount").textValue()
              set {_memo} to {_operation}.get(1).get("memo").textValue()
              set {_sender} to {_operation}.get(1).get("from").textValue()

              #
              # > Send the notifiaction message to the player.
              message "%getChatPrefix()% %{_sender}% sent you %{_amount}%:" to {_player}
              message "%getChatPrefix()% %{_memo}%" to {_player}

#
# > Function - setSteemPlayerToOnlineStatus
# > Sets the online status of a steem player to online
# > of offline depending on the given parameters.
# > Parameters:
# > <player>the player who should have a status change
# > <boolean>the current status of the player online=true,offline=false
function setSteemPlayerToOnlineStatus(player:player,online:boolean):

  #
  # > Get the synced steem account of the player.
  set {_steemaccount} to getSyncedAccount({_player})
  
  # > If the synced steem account of the player exists.
  if {_steemaccount} is set:
    if {_steemaccount} is not "":

      #
      # > Get all online players from the metadata.
      set {_onlineusers} to metadata value "onlineSteemUsers" of getDummy()

      #
      # > If the online players from the metadata aren't set, then create
      # > a new ArrayList which will contain online players.
      if {_onlineusers} is not set:
        set {_onlineusers} to new ArrayList()

      #
      # > If the player is online, then add the player.
      if {_online} is true:
        {_onlineusers}.add("%{_steemaccount}%|%{_player}%")

      #
      # > If the player is not online, then remove the player from the list.
      else:
        loop ...{_onlineusers}:
          if loop-value is "%{_steemaccount}%|%{_player}%":
            {_onlineusers}.remove(loop-value)
            stop loop

    #
    # > Save the new ArrayList to metadata, this is great since it is
    # > accessible from threaded functions while being stored in memory.
    set metadata value "onlineSteemUsers" of getDummy() to {_onlineusers}

#
# > Event - on join
# > Gets called once a player joins the game, it will
# > set the player's steem status to online.
on join:
  if {@enable-notifications} is true:
    setSteemPlayerToOnlineStatus(player,true)

#
# > Event - on join
# > Gets called once a player leaves the game, it will
# > set the player's steem status to offline.
on quit:
  if {@enable-notifications} is true:
    setSteemPlayerToOnlineStatus(player,false)
