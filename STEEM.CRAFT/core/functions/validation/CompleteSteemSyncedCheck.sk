#
# ==============
# CompleteSteemSyncedCheck.sk
# ==============
# CompleteSteemSyncedCheck.sk is part of the STEEM.CRAFT core functions.
# ==============

#
# > Function - CompleteSteemSyncedCheck
# > Starts a complete check for the player to have all necessary checks for
# > broadcasting to Steem.
# > Parameters:
# > <player> The player who should be checked.
# > <boolean> Should the function run the check in a thread? default=true
function CompleteSteemSyncedCheck(player:player,thread:boolean=true):
  set metadata value "SC|SteemCheck" of {_player} to "wait"
  if {_thread} is true:
    set metadata value "SC|SteemCheckThread" of getDummy() to {_player}
    $ thread
    CompleteSteemSyncedCheckAsync(metadata value "SC|SteemCheckThread" of getDummy())
  else:
    CompleteSteemSyncedCheckAsync({_player})

#
# > Function - CompleteSteemSyncedCheckAsync
# > Checks the synced account for the player and sends error messages
# > to the player if necessary to fix the problem.
# > Parameters:
# > <player> The player who should get checked for a synced Steem wallet.
function CompleteSteemSyncedCheckAsync(player:player):
  set {_steemname} to getSyncedAccount({_player})
  if {_steemname} is false:
    message "%getChatPrefix()% You have to sync your account first:" to {_player}
    message "%getChatPrefix()% /steemconnect <steem username>" to {_player}
    set metadata value "SC|SteemCheck" of {_player} to false

  else:

    #
    # > Set the response to a local usable variable.
    getAccount("%{_steemname}%",false)

    wait 1 tick

    while getAccountResponse("%{_steemname}%") is "wait":
      wait 1 tick

    #
    # > Set the response to a local usable variable.
    set {_account} to getAccountResponse("%{_steemname}%")

  #
  # > Check if the account is valid and usable for broadcasting.
  if validateSyncedSteemAccountUUID({_player},{_account}) is false:
    message "%getChatPrefix()% Your Steem account UUID doesn't match with yours." to {_player}
    message "%getChatPrefix()% /steemconnect <steem username>" to {_player}
    set metadata value "SC|SteemCheck" of {_player} to false

  if validateSyncedSteemAccountAuths({_player},{_account}) is false:
    message "%getChatPrefix()% The server has no authorisation to broadcast transactions for you." to {_player}
    message "%getChatPrefix()% /steemconnect <steem username>" to {_player}
    set metadata value "SC|SteemCheck" of {_player} to false
  if metadata value "SC|SteemCheck" of {_player} is "wait":
    set metadata value "SC|SteemCheck" of {_player} to {_steemname}

  #
  # > Delete the data again after 5 seconds.
  wait 5 seconds
  delete metadata value "SC|SteemCheck" of {_player}

#
# > Function - CompleteSteemSyncedCheckResult
# > Returns either "wait"(still working), false (not usable) or "Steemname" (usable)
# > Parameters:
# > <player> The player about which the result is needed.
function CompleteSteemSyncedCheckResult(player:player) :: object:
  return metadata value "SC|SteemCheck" of {_player}
