#
# ==============
# ViewSteemComment.sk
# ==============
# ViewSteemComment.sk is part of the STEEM.CRAFT core functions.
# ==============

import:
  com.fasterxml.jackson.databind.ObjectMapper
  java.util.ArrayList
  net.md_5.bungee.api.chat.ComponentBuilder
  net.md_5.bungee.api.chat.TextComponent
  net.md_5.bungee.api.chat.BaseComponent
  net.md_5.bungee.api.chat.ClickEvent
  net.md_5.bungee.api.chat.HoverEvent
  net.md_5.bungee.api.chat.HoverEvent$Action as HoverEventAction
  net.md_5.bungee.api.chat.ClickEvent$Action as ClickEventAction

#
# > Function - ViewSteemComment
# > Sends the comment into the chat of the player.
# > Also allows the player to view the post on steemit,
# > view replies, vote or reply on the given comment.
# > Parameters:
# > <player> The player who wants to view the comment.
# > <text> The author of the comment.
# > <text> The permlink of the comment that should be viewed.
function ViewSteemComment(player:player,author:text,permlink:text):

  set {_mapper} to new ObjectMapper()

  set {_request} to HashMap()
  set {_params} to new ArrayList()
  {_params}.add({_author})
  {_params}.add({_permlink})

  {_request}.put("jsonrpc", "2.0")
  {_request}.put("method", "condenser_api.get_content")
  {_request}.put("params", {_params})
  {_request}.put("id", 1)

  set {_requestID} to getRandomUUID()
  CustomSteemRequest({_mapper}.writeValueAsString({_request}),{_requestID})

  while CustomSteemRequestResult({_requestID}) is "wait":
    wait 1 tick

  set {_response} to CustomSteemRequestResult({_requestID})
  set {_body} to {_response}.get("result").get("body").textValue()
  set {_commenter} to {_response}.get("result").get("author").textValue()
  set {_children} to {_response}.get("result").get("children").doubleValue()

  message "%getChatPrefix()% %{_commenter}%'s comment:" to {_player}
  message "&7&m                                                                                &m" to {_player}
  message "&7%{_body}%" to {_player}
  message "&7&m                                                                                &m" to {_player}

  set {_message} to new ComponentBuilder("%getChatPrefix()% ")

  set {_component} to new TextComponent("[&6Browser&7] ")
  {_component}.setClickEvent(new ClickEvent(ClickEventAction.OPEN_URL!, "https://steemit.com/@%{_commenter}%/%{_permlink}%"))
  {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to view in browser." ).create()))
  {_message}.append({_component})

  set {_component} to new TextComponent("[&6Vote&7] ")
  {_component}.setClickEvent(new ClickEvent(ClickEventAction.RUN_COMMAND!, "/steem vote %{_commenter}% %{_permlink}% "))
  {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to vote on the comment." ).create()))
  {_message}.append({_component})

  #
  # > If there are replies, add a clickable command to view them.
  if {_children} >= 1:
    set {_component} to new TextComponent("[&6View Replies&7] ")
    {_component}.setClickEvent(new ClickEvent(ClickEventAction.RUN_COMMAND!, "/steem replies %{_commenter}% %{_permlink}% "))
    {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to reply." ).create()))
    {_message}.append({_component})

  set {_component} to new TextComponent("[&6Reply&7]")
  {_component}.setClickEvent(new ClickEvent(ClickEventAction.SUGGEST_COMMAND!, "/steem comment %{_commenter}% %{_permlink}% "))
  {_component}.setHoverEvent(new HoverEvent(HoverEventAction.SHOW_TEXT!, new ComponentBuilder( "&7Click here to reply." ).create()))
  {_message}.append({_component})
  {_player}.spigot().sendMessage({_message}.create())
